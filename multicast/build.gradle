plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id 'org.jetbrains.dokka'
    id 'kotlinx-atomicfu'
    id 'maven-publish'
}

group = GROUP
version = VERSION_NAME
apply from: rootProject.file("gradle/jacoco.gradle")
apply from: rootProject.file("gradle/targets.gradle")
targetCompatibility = 1.8
sourceCompatibility = 1.8

kotlin {
    jvm()
    targets {
        if (project.ext.ideaActive) {
            fromPreset(project.ext.ideaPreset, 'native')
        } else {
            fromPreset(presets.linuxX64, 'linuxX64')
            fromPreset(presets.iosArm64, 'iosArm64')
            fromPreset(presets.iosArm32, 'iosArm32')
            fromPreset(presets.iosX64, 'iosX64')
            fromPreset(presets.macosX64, 'macosX64')
            fromPreset(presets.mingwX64, 'windowsX64')
        }
    }

    sourceSets {
        all {
            languageSettings {
                languageVersion = "1.3"
                apiVersion = "1.3"
                progressiveMode = true
            }
        }
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation libraries.coroutinesCore
                implementation libraries.coroutinesCoreCommon
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
                implementation libraries.coroutinesTest
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test-junit')
            }
        }
        nativeMain {
            dependsOn commonMain
            dependencies {
                implementation libraries.coroutinesCoreNative
            }
        }
        nativeTest {
            dependsOn commonTest
        }
    }
}

task test(dependsOn:jvmTest)